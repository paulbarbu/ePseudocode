RESET_CURSOR_POS = "\x1b[H"
SHOW_CURSOR = "\x1b[?25h"
HIDE_CURSOR = "\x1b[?25l"
CLEAR = "\x1b[2J"
ENDC = "\x1b[0m"

LIVE = "o"
DEAD = "."

MAX_H = 20
MAX_W = 40

func get_initial(path)
    map = {}
    n = {}

    f=deschide(path, "r")

    cattimp n != fals executa
        map = map + n
        n = fciteste(f)
    sfcattimp

    inchide(f)
    ret map
sffunc

func make_map(initial)
    h = lung(initial)

    w = lung(initial[0])
    pt i=1; i<h; i=i+1 executa
        l = lung(initial[i])
        daca l > w atunci
            w = l
        sfdaca
    sfpt

    daca h > MAX_H sau w > MAX_W atunci
        ret fals
    sfdaca

    ci_x = ceiling(w/2) // center initial x
    ci_y = ceiling(h/2) // center initial y

    cm_x = ceiling(MAX_W/2)
    cm_y = ceiling(MAX_H/2)

    map = {}
    pt i=0; i<MAX_H; i=i+1 executa // lines
        map = map + {}
        pt j=0; j<MAX_W; j=j+1 executa // columns
            map[i] = map[i] + DEAD
        sfpt
    sfpt

    // insert initial configuration
    pt i=0; i<h; i=i+1 executa
        pt j=0; j<lung(initial[i]); j=j+1 executa
            map[i + cm_y - ci_y][j + cm_x - ci_x] = initial[i][j]
        sfpt
    sfpt

    ret map
sffunc

func print_map(m)
    scrie(CLEAR)
    scrie(RESET_CURSOR_POS)

    pt i=0; i<MAX_H; i=i+1 executa // lines
        pt j=0; j<MAX_W; j=j+1 executa // columns
            scrie(map[i][j])
        sfpt

        scrie("\n")
    sfpt
sffunc

func evolve_map(m)
    func get_num_live_neighbours(m, k, p)
        ct = 0
        dy = {-1, -1, 0, 1, 1,  1,  0, -1}
        dx = { 0,  1, 1, 1, 0, -1, -1, -1}

        pt i=0; i<8; i=i+1 executa
            row = k+dy[i]
            col = p+dx[i]

            daca row >= 0 si row < MAX_H si col >= 0 si col < MAX_W si LIVE == m[row][col] atunci
                ct = ct+1
            sfdaca
        sfpt

        ret ct
    sffunc

    old_map = m

    pt i=0; i<MAX_H; i=i+1 executa
        pt j=0; j<MAX_W; j=j+1 executa

            n = get_num_live_neighbours(old_map, i, j)

            daca LIVE == old_map[i][j] atunci
                daca n < 2 sau n > 3 atunci
                    m[i][j] = DEAD
                sfdaca
            altfel daca n == 3 atunci
                m[i][j] = LIVE
            sfdaca sfdaca
        sfpt
    sfpt

    ret m
sffunc

func main(argv)
    daca lung(argv) <= 0 atunci
        scrie("Please provide a starting configuration\n")
        ret 1
    sfdaca


    map = make_map(get_initial(argv[0]))

    daca map == fals atunci
        scrie("Invalid starting configuration\n")
        ret 1
    sfdaca

    ct = 0
    scrie(HIDE_CURSOR)
    cattimp ct < 100 executa
        print_map(map)
        map = evolve_map(map)
        ct = ct + 1
    sfcattimp
    scrie(SHOW_CURSOR)
sffunc
